plugins {
    id 'java'
    id "idea"
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.19.0'
    id 'com.avast.gradle.docker-compose' version '0.17.21'
    id 'com.softeq.gradle.itest' version '1.0.4'
    id 'com.google.cloud.tools.jib' version '3.4.2'
}

group = 'dev.diegourban'
version = '0.0.1-SNAPSHOT'
description = 'URL Shortener'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'

    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.8'
    compileOnly 'io.swagger.core.v3:swagger-annotations:2.2.42'
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-liquibase'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'commons-codec:commons-codec:1.21.0'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-liquibase-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'

    itestImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    itestImplementation 'org.springframework.boot:spring-boot-starter-liquibase-test'
    itestImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
    itestImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    itestImplementation 'org.junit.jupiter:junit-jupiter-api'
    itestImplementation 'org.junit.jupiter:junit-jupiter-engine'
    itestImplementation 'org.hamcrest:hamcrest:2.2'
    itestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApiGenerate {
    generatorName.set("spring")                // Spring Boot server generator
    inputSpec.set("$rootDir/api/url-shortener-oas.yaml") // Path to your OAS file
    outputDir.set(layout.buildDirectory.dir("generated").get().asFile.path)
    // Generated code location
    apiPackage.set("dev.diegourban.urlshortener.api")
    modelPackage.set("dev.diegourban.urlshortener.model")
    configOptions.set(
            [
                    "interfaceOnly" : "true",
                    "useSpringBoot3": "true",
                    "dateLibrary"   : "java8",
                    "library"       : "spring-boot",
                    "useTags"       : "true"
            ]
    )
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/src/main/java")
        }
    }
    itest {
        java {
            srcDir "$buildDir/generated/src/main/java"
            srcDir 'src/itest/java'
        }
        resources {
            srcDir 'src/itest/resources'
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

tasks.named('compileItestJava') {
    dependsOn tasks.named('openApiGenerate')
}

tasks.named('test') {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
}

idea {
    module {
        // prevents intellij marking directories as production source
        // might need `./gradlew cleanIdea idea` to recreate project files
        testSources.from(project.sourceSets.itest.java.srcDirs)
        testResources.from(project.sourceSets.itest.resources.srcDirs)
    }
}

itestSourceSet {
    compileClasspath = sourceSets.main.compileClasspath
    runtimeClasspath = sourceSets.main.runtimeClasspath
}

dockerCompose.isRequiredBy(integrationTest)

tasks.named('processItestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jib {
    from {
        image = "gcr.io/distroless/java25-debian13"
    }

    to {
        image = "ghcr.io/diegourban/url-shortener"
        tags = ["latest", System.getenv("GITHUB_SHA") ?: "local"]
        auth {
            username  = providers.environmentVariable("GHCR_USERNAME")
            password = providers.environmentVariable("GHCR_TOKEN")
        }
    }

    container {
        mainClass = "dev.diegourban.url_shortener.UrlShortenerApplication"
        ports = ["5000"]
        jvmFlags = [
                "-XX:+UseContainerSupport",
                "-XX:MaxRAMPercentage=75.0"
        ]
        creationTime = "USE_CURRENT_TIMESTAMP"
    }
}

